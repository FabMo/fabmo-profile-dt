'!FABMO!name: Z-Zero
'!FABMO!description: Set Z-Zero with Zeroing Plate; Use after installing new cutter
' Name: Z-Zero
' Description: 
'   Set Z-Zero with Zeroing Plate; Use after installing new cutter [C2]
' Platform: Desktop
' Author: Ted Hall, Ryan Sturmer
' History:
'   2016/03/01 - Initial version (th)
'   2017/09/25 - Refactored (th)

SU, 0                         ' Set to Inches; Units will auto-revert to current starting units at end of file

'--- Settings ---             
$current_cutter_Zoffset := .25' Initializes permanent variables if not already set
$z_backoff := .05
$ZZeroInput := 1              ' Later also using system variable %(51) to read input; ... a temporary kludge
$ZZeroPlateThickness := 0.118 ' 0.118" for std plate; 0.57" for pressure plate
  ' To only use pressure plate, change &ZZeroInput to 5
  ' ... adjust plate thickness to 0.57
  ' ... and change %(51) to %(55) in check_input subroutine
$ZZeroSearchSpeedFast = 0.5

&ZZeroSearchDistance = -6.0 
&ZZeroSearchSpeedSlow = 0.1


MS, 4, 4					  ' Speeds will auto-revert back to current values at end of file
JS, 5, 5

' -------- Main Program Section --------
GOSUB check_input
GOSUB find_z_zero
END

' --- Subroutines ---
find_z_zero:    
   PZ, %(3) + &ZZeroSearchDistance, $ZZeroSearchSpeedFast, $ZZeroInput
   JZ, %(3) + $z_backoff 
   PZ, %(3) -2*$z_backoff, &ZZeroSearchSpeedSlow, $ZZeroInput
   JZ, %(3) + $z_backoff 
   VA,,, $z_backoff + $ZZeroPlateThickness
   RETURN

missed_target:
    FAIL "Target Not Triggered!"                           

check_input:
    IF %(51) = 1 THEN GOTO check_input_fail
	RETURN

check_input_fail:
    FAIL "An Input is already triggered. Cannot zero the tool. Move Off and Retry."
